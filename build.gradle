import org.ajoberstar.grgit.Grgit

plugins {
    id "org.jetbrains.intellij" version "0.4.1"
    id "org.ajoberstar.grgit" version "3.1.1"
    id "java"
    id "idea"
}

group 'net.ishchenko.idea.nginx'
version '0.1.9'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

sourceSets {
    main {
        java.srcDirs 'src', 'gen'
        resources.srcDir 'resources'
    }
    test {
        java.srcDir 'tests'
    }
}

intellij {
    version '2019.2' //IntelliJ IDEA dependency
    updateSinceUntilBuild false
    pluginName 'idea-nginx-support'

    publishPlugin {
        username project.hasProperty('user') ? user : ''
        password project.hasProperty('pass') ? pass : ''
    }
}

task generateDocumentation(type: JavaExec) {
    main = 'net.ishchenko.idea.nginx.KeywordsFromSourcesGenerator'
    classpath = sourceSets.main.runtimeClasspath
}

task getRepos() {
    doLast {
        def path = 'vendor/nginx'
        if (!file(path).exists()) {
            Grgit.clone(dir: file(path), uri: 'https://github.com/nginx/nginx.git')
        } else {
            Grgit.open(dir: file(path)).pull()
        }
        def luaPath = 'vendor/lua-nginx-module'
        if (!file(luaPath).exists()) {
            Grgit.clone(dir: file(luaPath), uri: 'https://github.com/openresty/lua-nginx-module.git')
        } else {
            Grgit.open(dir: file(luaPath)).pull()
        }
    }
}
